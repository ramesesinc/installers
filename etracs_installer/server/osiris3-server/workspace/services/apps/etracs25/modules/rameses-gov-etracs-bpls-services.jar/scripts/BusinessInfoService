import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.services.extended.*;

public class BusinessInfoService extends ActiveCrudService {

	@ActiveDB("business")
	def em;

	@ActiveDB("businesspermit")
	def bperm;

	@Service("DateService")
	def dateSvc;

	@Env
	def env;

	@Service("SequenceService")
	def seqSvc;

	@Service("Var")
	def sysvar;

	@Service("LOVService")
	def LOV;

	/**********************************************************
	* called by new application or by data capture.
	* BIN naming standard = {LGUCODE,3}{YEAR,4}{MONTH,2}{SEQ,5}
	***********************************************************/ 
	public void beforeCreate(o) {
		if(!o.objid) {
			o.objid = "BUS"+new UID();
		}	
		o.activeyear = o.activeyear;
		if(o.state!='CAPTURED') o.state = "PROCESSING";
		o.taxcredit = 0;
		o.lastqtrpaid = 0;
		o.appcount = 0;
		o.currentapplicationid = null;
		o.user = [objid: o.createdby];
		if(!o.bin) {
			def lgucode = sysvar.lgu_objid;
			if(!lgucode)
				throw new Exception("LGU code is null. Please check lgu_objid in sysvar");
			lgucode = lgucode.replace("-", "");	
			def yr = dateSvc.serverYear;
			def mnth = dateSvc.serverMonth;
			def smon = ( mnth < 10 ) ? "0"+mnth : mnth+"";
			def prefix = "B"+lgucode+yr+smon;
			o.bin = prefix+seqSvc.getNextFormattedSeries(prefix);
		}
	}

	public void afterOpen(o) {
		if(o.orgtype) o.orgtypename = LOV.ORG_TYPES[o.orgtype];
		o.barcode = '51005:'+o.bin;

		//check if there is permit
		def perm = bperm.findActivePermit( [businessid: o.objid, activeyear: o.activeyear]);
		if(perm) {
			o.permit = perm;
		}
		if(o.businessaddress?.objid) {
			def a = em.read( o.businessaddress, "address" ); 
			o.businessaddress.putAll( a );
		}
	}

	/**********************************************************
	* loads only the most active lines of business based on the
	* active year of the business
	***********************************************************/ 
	@ProxyMethod
	public def getLobs(o) {
		return em.getLobs( o );
	}	

	@ProxyMethod
	public def getAppInfos(o) {
		def infos = em.getAppInfos( o );
		infos.each {
			it.value = formatInfo(it);
		}
		return infos;
	}	

	@ProxyMethod
	public def getAssessmentInfos(o) {
		def infos = em.getActiveAssessmentInfos( o );
		infos.each {
			it.value = formatInfo(it);
		}
		return infos;
	}	


	/********************************************************
	* activate - this method is called when an application is 
	* released. It will break the application into the app infos
	* lobs and assessment infos, and change the state of the
	* business from PROCESSING->ACTIVE. 
	*********************************************************/
	@ProxyMethod
	public def activate(o) {
		if(o.state=='CAPTURED') {
			o.lobs.each {
				em.create(it, "lob");
			}
		}
		em.update(o);
	}

	/********************************************************
	* management of infos
	*********************************************************/
	private void convertInfo(o) {
		def value = o.value;
		if( value == null ) return;
		switch(o.attribute.datatype) {
			case "decimal":
				o.decimalvalue = Double.parseDouble(value+"");
				break;
			case "integer":
				o.intvalue = Integer.parseInt(value+"");
				break;
			case "string_array":	
			case "string":
				o.stringvalue = (String)value;
				break;
			case "boolean":
				value = ( value == 1 || value == true ) ? "true" : "false";
				boolean b =  Boolean.parseBoolean(value);
				o.boolvalue = (b) ? 1 : 0; 
				break;
		} 
	}

	private def formatInfo(x)  {
		switch(x.attribute.datatype) {
			case "decimal" : return x.decimalvalue; break;
			case "integer" : return x.intvalue; break;
			case "string_array" : return x.stringvalue; break;
			case "string"  : return x.stringvalue; break;
			case "boolean" : 
				if(x.boolvalue == 0) return false;
				if(x.boolvalue == 1) return true;
				return x.boolvalue; 
				break;
		}
	}


	/****************************************************************
	* different finder and open methods
	* The open for Application is specific for applications requiring
	* to open an existing info. The open is not used becuase it
	* will fire the BPApplicationInterceptor whereas openForApplication
	* does not.
	****************************************************************/
	@ProxyMethod
	public def openForApplication(o) {
		def z = open(o);
		z.appinfos = getAppInfos(o);
        z.lobs = getLobs(o);
        z.assessmentinfos = [];
		return z;
	}

	@ProxyMethod
	public def findForWorkflow(o) {
		def z = open(o);
		z.appinfos = getAppInfos(o);
        z.lobs = getLobs(o);
        z.assessmentinfos = getAssessmentInfos(o);
		return z;
	}

	@ProxyMethod
	public def findByBIN( o ) {
		def z = em.findByBIN(o);
		if(!z)
			throw new Exception("BIN not found. " + o.bin );
		afterOpen(z);
		z.appinfos = getAppInfos(o);
        z.lobs = getLobs(o);
        z.assessmentinfos = getAssessmentInfos(o);
		return z;
	}

	@ProxyMethod
	public def getRedflags(o) {
		return em.getRedflags(o);
	}
	
	@ProxyMethod
	public def getApplicationList(o) {
		return em.getApplicationList(o);
	}
	
	//called during release
	@ProxyMethod
	public def updateInfos( o ) {
		if(o.apptype.matches('NEW|RENEW')) {
			o.state = 'ACTIVE';
			em.removeInfos([objid:o.objid]);
			em.removeLOB([objid:o.objid]);
			em.removeAssessmentInfos([objid:o.objid]);
			o.lobs.each {
				it.businessid = o.objid;
				em.create(it, "lob");				
			}
			o.appinfos.each {
				it.objid = "BUSINFO"+new UID();
				it.businessid = o.objid
				convertInfo(it);
				em.create(it, "info");
			}
			o.assessmentinfos.each {
				it.businessid = o.objid;
				it.objid = "BUSASSMT"+new UID();
				convertInfo(it);
				em.create(it, "assessment_info");
			}
			em.update(o);			
		}
		else if( o.apptype == 'RETIRE') {
			o.state = 'RETIRED';
			em.removeInfos([objid:o.objid]);
			em.removeLOB([objid:o.objid]);
			em.removeAssessmentInfos([objid:o.objid]);
			o.lobs.each {
				it.businessid = o.objid;
				it.assessmenttype = 'RETIRE';
				em.create(it, "lob");				
			}
			em.update(o);
		}
		else if(o.apptype == 'AMENDLOB') {
			o.lobs.each {
				if(it.assessmenttype == 'NEW') {
					it.businessid = o.objid;
					em.create(it, "lob");				
				}
				else if(it.assessmenttype == 'RETIRE') {
					em.delete( it, "lob" );
				}
				else if(it.assessmenttype == 'REMOVE') {
					em.delete( it, "lob" );
				}
				else if(it.assessmenttype == 'RECLASSIFY') {
					em.update( it, "lob" );
				}
			}
			def b = em.read( o );
			b.state = 'ACTIVE';
			em.update(b);
		}

	}

	@ProxyMethod
	public def updateBusinessInfo( def o ) {
		def x = [:];
		x.putAll( em.read(o) );
		x.businessid = o.objid;
		x.objid = 'BUSINFO'+new UID();
		x.dtfiled = dateSvc.serverDate;
		x.filedby = [objid:env.USERID , name:env.USER];
		em.create( x, "changeinfo");
		em.update( o );
	}

	@ProxyMethod
	public void updateLobs( def o ) {
		em.removeLOB([objid:o.objid]);	
		o.lobs.each {
			it.businessid = o.objid;
			em.create(it, "lob");				
		}	
	}

}
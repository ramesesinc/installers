package rptbilling;

import rptbilling.*;
import java.util.*;
import com.rameses.rules.common.*;

global RuleAction action;




rule "apply-incentive"
	agenda-group "ASSIGN_PERIOD"
	salience -50
	no-loop
	when
		INC : RPTIncentive($fromyear:fromyear, $toyear:toyear)
		RLI : RPTLedgerItemFact (  rptledger != null, year >= $fromyear, year <= $toyear  )
	then	
		System.out.println("apply-incentive");
		Map map = new HashMap();
		map.put("incentive", INC);
		map.put("rptledgeritem", RLI);
		action.execute( "apply-incentive", map, drools );
end



rule "apply-partial-payment-made"
	agenda-group "ASSIGN_PERIOD"
	salience -100
	no-loop
	when
		PP: PartialPaymentMade()
		RLI: RPTLedgerItemFact (  rptledger != null, firstitem == true )
	then	
		System.out.println("apply-partial-payment-made");
		Map map = new HashMap();
		map.put("partialpayment", PP);
		map.put("rptledgeritem", RLI);
		action.execute( "apply-partial-payment-made", map, drools );
		retract(PP);
end


rule "apply-full-payment"
	agenda-group "AFTER_ASSIGN_REVENUE"
	salience -50

	when 
		PP : PartialPayment($idx:idx, $amount:amount)
		RLI: RPTLedgerItemFact( rptledger != null, idx == $idx, total <= $amount  )
	then 	
		System.out.println("apply-full-payment -> " + RLI.getIdx() + " YEAR -> " + RLI.getYear() + " QTR -> " + RLI.getQtr() + " Amount : " + PP.getAmount());
		PP.setAmount( PP.getAmount() - RLI.getTotal() );
		PP.setIdx( PP.getIdx() + 1);
		update(PP);
end



rule "apply-partial-payment"
	agenda-group "AFTER_ASSIGN_REVENUE"
	salience -50
	no-loop

	when 
		PP : PartialPayment($idx:idx, $amount:amount)
		RLI: RPTLedgerItemFact( rptledger != null, idx == $idx, total > $amount  )
	then 	
		System.out.println("apply-partial-payment -> " + RLI.getIdx() + " YEAR -> " + RLI.getYear() + " QTR -> " + RLI.getQtr() + " Amount : " + PP.getAmount());
		Map map = new HashMap();
		map.put("partialpayment", PP);
		map.put("rptledgeritem", RLI);
		action.execute( "apply-partialpayment", map, drools );
		PP.setAmount(0.0);
		update(PP);
end

rule "remove-unpartialled-item"
	agenda-group "AFTER_ASSIGN_REVENUE"
	salience -100
	no-loop

	when 
		PP : PartialPayment($idx:idx, amount == 0)
		RLI: RPTLedgerItemFact( rptledger != null, idx > $idx)
	then 	
		System.out.println("remove-unpartialled-item -> " + RLI.getIdx() + " YEAR -> " + RLI.getYear() + " QTR -> " + RLI.getQtr() );
		Map map = new HashMap();
		map.put("rptledgeritem", RLI);
		action.execute( "remove-unpartialled-item", map, drools );
		retract(RLI);
end

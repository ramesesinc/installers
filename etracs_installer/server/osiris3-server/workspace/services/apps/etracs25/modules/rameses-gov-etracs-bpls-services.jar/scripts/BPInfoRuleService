import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.rules.common.*;

import bpls.facts.*;
import bpls.actions.*;

public class BPInfoRuleService  {

	@Resource("RuleService")	
	def ruleSvc;

	@ActiveDB("rule")
	def ruleDB;

	@ActiveDB("lob")
	def lob;

	@ActiveDB("businessvariable")
	def bv;

	@Service("BusinessRequirementService")
	def br;

	@Service("DateService")
	def dateSvc;

	@Service("NumberService")
	def numberSvc;

	@Service("BPFactBuilder")
	def bpFacts;

	String RULESET = "bpinfo";

	def comparator = { agg, oldvalue, newvalue ->
		switch(agg) {
			case "COUNT":
				return (oldvalue + 1);

			case "SUM":
				return oldvalue + newvalue;

			case "MIN":
				if( oldvalue <= newvalue  )		
					return oldvalue;
				else
					return newvalue;
			case "MAX":
				if( oldvalue >= newvalue  )		
					return oldvalue;
				else
					return newvalue;
		}
		return 0;
	}

	def buildFacts( request ) {
		def o = request.entity;
		def facts = [];
		def currDate = dateSvc.serverDate;
		def dt = currDate;
		if(o.dtfiled && !(o.dtfiled instanceof java.util.Date)) {
			dt = java.sql.Date.valueOf( o.dtfiled );
		}
		facts << new EffectiveDate( dt );
		facts << new CurrentDate(currDate);
		bpFacts.buildApplication( o, facts );
		return facts;
	}

	def buildActions(def request) {
		def actions = new RuleAction();
		actions.addCommand( "ask_business_info", new AskBusinessInfo( infotype:'info', BV:bv, request:request ));
		actions.addCommand( "assert_business_info", new AssertBusinessInfo(request:request, BV:bv, comparator: comparator));
		actions.addCommand( "add_requirement", new AddRequirement(request:request, BR: br));
		actions.addCommand( "print_test", new PrintTest());
		return actions;
	}

	@ProxyMethod
	public def execute(def o) throws Exception {
		//we need to remove first infos where phase==null || phase > 1
		o.infos.removeAll( o.infos.findAll{it.phase==null || it.phase > 1} );
		def request = [newinfos:[],  vars:[:], entity: o, requirements:[] ]; 
		request.facts = buildFacts(request);
		request.actions = buildActions( request );
		def grps = ruleDB.getRulegroups( [ruleset: RULESET] );
		int phase = 0;
		for( g in grps ) { 
			request.phase = phase;
			request.vars.clear();
			ruleSvc.execute( RULESET, request.facts, request.actions, g.name );
			//if first phase and there is entry in newinfos break it.
			if(phase <= 1 && request.newinfos) break;
			phase++;
		}
		if(phase == 1) phase = 2;
		return [infos:request.newinfos, phase: phase, requirements:request.requirements];
	}
	
}


import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.util.*;

class CancelledFAASService
{
	@PersistenceContext('rpt')
	def em
    
	@Env
	def env
	
	@Service('TxnRefService')
    def txnRef

    @Service('WorkflowStateService')
    def workflowRef
    
    @Service('DateService')
    def dateSvc

    @Service('SequenceService')
    def seqSvc

    @Service('RPTUtil')
    def util 
    
    @Service('CancelTDReasonService')
    def reasonSvc

    @Service('FAASService')
    def faasSvc 

    def STATE_DRAFT 		= 'DRAFT'
    def STATE_FORAPPROVAL 	= 'FORAPPROVAL'
    def STATE_APPROVED 		= 'APPROVED'
    
	def SCHEMA_NAME = 'cancelledfaas'
    

	@ProxyMethod
    public def getList( params ) {
    	params.searchtext = (params.searchtext ? params.searchtext : '') + '%'
        def filters = [
            ' and f.tdno LIKE $P{searchtext}',
            ' and f.owner_name LIKE $P{searchtext}',
            ' and r.fullpin LIKE $P{searchtext}',
        ]
        return util.getListFromQuery( 'cancelledfaas:getList' , filters, params, 'cf.objid' )
    }


    @ProxyMethod
    public def createCancelledFaas(entity){
        txnRef.checkReference(entity.faas.objid)
        validateFaas(entity.faas)
        entity.faasid = entity.faas.objid 
        entity.txnno = seqSvc.getNextFormattedSeries('CancelledFAAS')
        entity.txndate = dateSvc.serverDate
        txnRef.insertRef( entity.faas.objid, entity.objid, 'FAAS is currently referenced by Cancellation No. ' + entity.txnno + '.')

        entity.state = STATE_DRAFT
        workflowRef.insert(entity)

        def xentity = EntityUtil.mapToField(entity)
        em.validate(SCHEMA_NAME, xentity)
        em.create(SCHEMA_NAME, xentity)
        return entity
    }


    @ProxyMethod
    public def updateCancelledFaas( entity) {
        def old = openCancelledFaas(entity.objid)
        if (old.state == STATE_APPROVED ) 
            throw new Exception('Document has already been approved.')

        def xentity = EntityUtil.mapToField(entity)
        em.validate(SCHEMA_NAME, xentity)
        em.update(SCHEMA_NAME, xentity)
        return entity 
    }


    @ProxyMethod
    public void deleteCancelledFaas( entity ) {
        txnRef.delete(entity.objid)
        workflowRef.deleteAll(entity.objid)    
        em.delete(SCHEMA_NAME, entity)
    }


    @ProxyMethod
    public def openCancelledFaas( objid ) {
        def entity = em.sqlContext.createNamedQuery('cancelledfaas:open')
                        .setParameter('objid', objid).singleResult    
        if (!entity)
            throw new Exception('Record does not exist or has already been deleted.')
        return entity 
    }


    @ProxyMethod
    public def submitForApproval(entity){
        entity.state = STATE_FORAPPROVAL
        workflowRef.insert(entity)
        updateCancelledFaas(entity)
        return entity 
    }


    @ProxyMethod
    public def disapproveFaasCancellation( entity ) {
        workflowRef.delete(entity)    
        entity.state = STATE_DRAFT
        updateCancelledFaas(entity)
        return entity 
    }


    @ProxyMethod
    public def approveFaasCancellation( entity ) {
        entity.state = STATE_APPROVED
        workflowRef.insert(entity)   
        updateCancelledFaas(entity) 
        cancelFaas(entity.faas)
        txnRef.delete(entity.objid)
        return entity 
    }


    @ProxyMethod
    public def getCancelReasons(  ) {
        return reasonSvc.getList([:])    
    }



    void validateFaas(faas){
        //validate state 
        def state = faasSvc.getState(faas.objid)
        if ( state == 'CANCELLED') throw new Exception('FAAS has already been cancelled.')
        if ( state != 'CURRENT') throw new Exception('FAAS is not yet approved. Only current record is allowed.')

        //if land check that all improvements are cancelled
        if (faas.rputype == 'land'){
            def improvements = em.sqlContext.createNamedQuery('cancelledfaas:getNonCancelledImprovements')
                    .setParameter('realpropertyid', faas.realpropertyid ).resultList

            if (improvements.size() == 1) {
                throw new Exception('Improvement with TD No. ' + improvements[0].tdno + ' is not yet cancelled. Cancel this improvement first before cancelling this land.')
            }
            else if( improvements.size() > 1 ){
                def tdnos = improvements.tdno.join(', ')
                throw new Exception('Improvements with TD No(s). ' + tdnos + ' are not yet cancelled. Cancel these improvements first before cancelling this land.')
            }
        }
    }

    void cancelFaas(faas){
        em.sqlContext.createNamedExecutor('cancelledfaas:cancelFaas').setParameter('objid', faas.objid).execute()
        em.sqlContext.createNamedExecutor('cancelledfaas:cancelRpu').setParameter('objid', faas.rpuid).execute()
        if (faas.rputype == 'land'){
            em.sqlContext.createNamedExecutor('cancelledfaas:cancelRealProperty').setParameter('objid', faas.realpropertyid).execute()
        }
    }
}


import com.rameses.annotations.*
import com.rameses.common.*
import java.rmi.server.UID;
import com.rameses.util.*;
import com.rameses.io.*;
import com.rameses.services.extended.*;

class IndividualEntityService extends ActiveCrudListService {

	@ActiveDB("entityindividual")
	def	em; 

	@ActiveDB("entity")
	def	entity; 

	@Env
	def env;

	@Service('DateService')
	def dtSvc;

	@Service("LOVService")
	def LOV;

	@Service('SequenceService')
	def seqSvc;

	def INDEX_CHAR_COUNT = 300;

	String pagingKeys = 'e.objid'

	@ProxyMethod
	public def getColumns( params ) {
		return [
			[name:'entityno', caption:'Entity No', width:80, resizable:false],
			[name:'name', caption:'Name'], 
			[name:'address.text', caption:'Address'], 				
			[name:'gender', caption:'Gender', width:50, resizable:false, alignment:'center'], 
			[name:'birthdate', caption:'Birth Date', width:70, resizable:false, alignment:'center']
		]; 
	}

	void updateName( o ) {
		o.name = o.lastname+', '+o.firstname; 
		if (o.middlename) o.name += ' '+o.middlename;	
		def len = o.name.length()
		if (len > INDEX_CHAR_COUNT) len = INDEX_CHAR_COUNT
		o.entityname = o.name.substring(0, len);		
	}

	public void beforeCreate(o) {
		o.entityno = "I"+seqSvc.getNextFormattedSeries( "INDIVIDUAL_ENTITY" ); 
		if(!o.address.objid) o.address.objid = 'ENTADDR'+new UID();
		o.address.parentid = o.objid;
		o.type = 'INDIVIDUAL';
		updateName(o);
		entity.create( o );
	}

	public void afterCreate(o) { 
		if(o.photo) {
			updatePhoto(o);			
		}
		entity.create( o.address, "address" ); 
	}

	public void beforeUpdate(o) {
		updateName(o);
		if(!o.address?.objid) o.address.objid = 'ENTADDR'+new UID();		
		o.address.parentid = o.objid;
		entity.update( o );
		entity.save( o.address, "address" ); 
	}

	public void afterRemoveEntity(o) {
		entity.delete( o );
	}

	public void afterOpen( o ) {
		def z = entity.read( o );
		o.putAll( z );
		if (o.birthdate){
			o.age = dtSvc.calculateAge( o.birthdate );
		}		
		if( o.gender ) {
			o.gendername = LOV.GENDER.get( o.gender );
		}
		if( o.civilstatus ) {
			o.civilstatusname = LOV.CIVIL_STATUS.get( o.civilstatus );
		}
		if(o.address?.objid) {
			def a = entity.read( o.address, "address");
			o.address.putAll(a);
		}
		else {
			o.address = [:];	
		}
	}

	@ProxyMethod
	public def updatePhoto( def o ) {
		em.updatePhoto( o );
	}

	@ProxyMethod
	public def getPhoto( def o ) {
		return em.findPhoto( o )?.photo;
	}

	@ProxyMethod
	public def getCitizenshipList(o) {
		o.searchtext += "%";
		o._limit = 8;
		return em.getCitizenshipList( o )*.citizenship;
	}

	@ProxyMethod
	public def getReligionList(o) {
		o.searchtext += "%";
		o._limit = 8;
		return em.getReligionList( o )*.religion;
	}

	@ProxyMethod
	public def getProfessionList(o) {
		o.searchtext += "%";
		o._limit = 8;
		return em.getProfessionList( o )*.profession;
	}

	@ProxyMethod
	public void editName(o) {
		updateName(o);
		entity.update( o );
		em.updateName( o );
	}

}
